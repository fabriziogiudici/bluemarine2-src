            SELECT DISTINCT *
            WHERE
              {
                        ?record         a                                         mo:Record.
                      { ?record         bmmo:importedFrom                         ?source.        }
@fallback@      UNION { ?record         bmmo:importedFrom                         ?fallback.      }
@fallback@      MINUS { []              bmmo:alternateOf                          ?record;
@fallback@                              bmmo:importedFrom                         ?source.        }
                MINUS { ?record         bmmo:alternatePickOf                      [].             }

                        ?track          a                                         mo:Track;
                                        rdfs:label                                ?label;
                                        mo:track_number                           ?track_number.
@artist@              { ?track          foaf:maker                                ?artist.        }
@artist@        UNION { ?track          foaf:maker/rel:collaboratesWith           ?artist.        }

                        ?record         mo:track                                  ?track;
                                        rdfs:label                                ?record_label.
             OPTIONAL { ?record         bmmo:diskNumber                           ?disk_number.   }
             OPTIONAL { ?record         bmmo:diskCount                            ?disk_count.    }

                        ?signal         a                                         mo:DigitalSignal;
                                        mo:published_as                           ?track;
                                        mo:duration                               ?duration.

                        ?audioFile      a                                         mo:AudioFile;
                                        mo:encodes                                ?signal;
                                        bmmo:path                                 ?path.
             OPTIONAL { ?audioFile      bmmo:fileSize                             ?fileSize.      }
              }
            ORDER BY
                ?record_label
                ?disk_number
                ?track_number
                ?label
