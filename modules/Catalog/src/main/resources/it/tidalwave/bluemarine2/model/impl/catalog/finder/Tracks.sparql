        SELECT DISTINCT *
        WHERE
          {
                       ?track                 a                                  mo:Track;
                                              bmmo:importedFrom                  ?source;
                                              rdfs:label                         ?label;
                                              mo:track_number                    ?track_number.
            OPTIONAL { ?track                 bmmo:diskNumber                    ?disk_number.      }
            OPTIONAL { ?track                 bmmo:diskCount                     ?disk_count.       }
@artist@             { ?track                 foaf:maker                         ?artist.           }
@artist@    UNION    { ?track                 foaf:maker/rel:collaboratesWith    ?artist.           }

                       ?record                a                                  mo:Record;
                                              mo:track                           ?track;
                                              rdfs:label                         ?record_label.
            MINUS    { bmmo:alternativeItems  bmmo:includes                      ?record.           }

                       ?signal                a                                  mo:DigitalSignal;
                                              mo:published_as                    ?track;
                                              mo:duration                        ?duration.

                       ?audioFile             a                                  mo:AudioFile;
                                              mo:encodes                         ?signal;
                                              bmmo:path                          ?path.
            OPTIONAL { ?audioFile             bmmo:fileSize                      ?fileSize.         }
          }
        ORDER BY
            ?record_label
            ?disk_number
            ?track_number
            ?label
